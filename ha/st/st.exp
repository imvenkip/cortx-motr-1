#!/usr/bin/env expect
### ------------------------------------------------------------------
### System test for HA notification interface.
###
### Assumes that `m0mero' kernel module is already inserted.
### ------------------------------------------------------------------

proc full_path {name} {
    global argv0
    return [file join [file dirname $argv0] $name]
}

proc wait_for {sid name} {
    expect -i $sid {
        timeout {
            send_error "$name: timed out\n"
            return 110  ;# ETIMEDOUT
        }
        eof
    }
    set rc [lindex [wait -i $sid] 3]
    if {$rc != 0} {
	send_error "$name: exit status $rc\n"
    }
    return $rc
}

set timeout 30

# Start server.
spawn sudo [full_path dummyha] -l 0@lo:12345:35:1
set dummyha $spawn_id

# Check that server is ready.
expect -i $dummyha "Enter"

# Start client.
spawn sudo [full_path m0note] -l 0@lo:12345:35:2 -a 0@lo:123345:35:1
set m0note $spawn_id

# Wait for client to send notification and terminate.
set rc1 [wait_for $m0note "m0note"]
if {$rc1 == 0} {
    expect -i $dummyha "Received notification"
}

# Send '\n' to server and wait for it to quit.
send -i $dummyha "\n"
set rc2 [wait_for $dummyha "dummyha"]

exit [expr $rc1 || $rc2]

# Local Variables:
# mode: tcl
# End:
